doc
  Reader provides read-only environment passing via effects.

  Use `Reader.with_reader` at boundaries and `Reader.ask`/`asks`
  in business logic that depends on shared context.
effect Reader R
  doc
    Read the current environment value.

      Reader.ask()
  fn ask() -> R

doc
  Run `f` with `Reader.ask()` bound to `env`.

    Reader.with_reader(41, || Reader.ask() + 1)   -- => 42
fn with_reader(env: R, f: fn() -[Reader R, e]> T) -[e]> T
  handle f()
    Reader.ask() -> resume env

doc
  Read the environment and transform it with `f`.

    Reader.with_reader(41, || Reader.asks(|x| x + 1))   -- => 42
fn asks(f: fn(R) -> T) -[Reader R]> T
  f(Reader.ask())
